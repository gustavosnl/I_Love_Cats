apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.glima.ilovecats"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def apiKey = retrieveApiKey()

        debug {
            minifyEnabled false
            buildConfigField "String", "SERVER_URL", "\"https://api.thecatapi.com/v1/\""
            buildConfigField "String", "API_KEY", "\"$apiKey\""

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", "\"https://api.thecatapi.com/v1/\""
            buildConfigField "String", "API_KEY", "\"$apiKey\""
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions.unitTests {
        includeAndroidResources = true
    }
    useLibrary 'android.test.runner'

}

private Object retrieveApiKey() {
    def apiKey = System.env.CAT_API_KEY
    if (apiKey != null) return apiKey
    else throw new InvalidUserDataException("You must have to define CAT_API_KEY as environment variable.")
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation project(':data')
    implementation project(':domain')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_extensions_version"


    //NAVIGATION
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"


    //DI
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    implementation "org.koin:koin-android:$koin_version"


    //NETWORK
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofit_coroutines_adapter_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"


    //PAGING
    implementation "androidx.paging:paging-runtime:$paging_version"


    //IMAGE LOADING
    implementation "com.github.bumptech.glide:glide:$glide_version"


    testImplementation "androidx.test.ext:junit-ktx:$androidx_test_junit_version"
    testImplementation "androidx.test:core-ktx:$androidx_test_core_version"
    testImplementation "androidx.test:core:$androidx_test_core_version"
    testImplementation "org.robolectric:robolectric:$roboletric_vesion"
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "androidx.arch.core:core-testing:$arch_testing_version"

    testImplementation 'junit:junit:4.13'

    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'


}

